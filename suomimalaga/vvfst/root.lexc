Multichar_Symbols

! Sanaluokat
[Ln]  ! nimisana
[Lee] ! erisnimi - etunimi
[Les] ! erisnimi - sukunimi
[Lep] ! erisnimi - paikannimi
[Lem] ! erisnimi - muu erisnimi
[Ll]  ! laatusana
[Lnl] ! nimi_laatusana
[Lt]  ! teonsana
[Lh]  ! huudahdussana
[Lp]  ! etuliite
[La]  ! lyhenne
[Ls]  ! seikkasana
[Lc]  ! sidesana
[Lu]  ! lukusana
[Lr]  ! asemosana
[Ld]  ! suhdesana
[Lk]  ! kieltosana

! Vokaalisointu
[Va]
[Vä]
[Vaä]

! Merkkijonoparametrit
[Xp]  ! perusmuoto
[Xr]  ! rakenne
[X]   ! parametrin päätemerkki

! Sijamuodot
[Sn]   ! nominatiivi
[Sg]   ! genetiivi
[Sp]   ! partitiivi
[Ses]  ! essiivi
[Str]  ! translatiivi
[Sine] ! inessiivi
[Sela] ! elatiivi
[Sill] ! illatiivi
[Sade] ! adessiivi
[Sabl] ! ablatiivi
[Sall] ! allatiivi
[Sab]  ! abessiivi
[Sko]  ! komitatiivi
[Sin]  ! instruktiivi
[Ssti] ! kerronto_sti
[Sak]  ! akkusatiivi

! Luvut
[Ny]  ! yksikkö
[Nm]  ! monikko

! Omistusliitteet
[O1y] ! yksikön 1.
[O2y] ! yksikön 2.
[O1m] ! monikon 1.
[O2m] ! monikon 2.
[O3] ! yksikön/monikon 3.

! Vertailumuodot
[Cc] ! voittoaste
[Cs] ! yliaste

! Liitepartikkelit
[Fkin] ! -kin
[Fkaan] ! -kaan

! Tapaluokat
[Tn1] ! 1. nimitapa
[Tn1p]! 1. nimitapa, pitkä muoto
[Tn2] ! 2. nimitapa
[Tn3] ! 3. nimitapa
[Tn5] ! 5. nimitapa
[Tt]  ! tositapa
[Te]  ! ehtotapa
[Tk]  ! käskytapa
[Tm]  ! mahtotapa

! Tekijät
[P1]  ! 1. persoona
[P2]  ! 2. persoona
[P3]  ! 3. persoona
[P4]  ! 4. persoona

! Aikamuodot
[Ap]  ! kestämä (preesens)
[Ai]  ! kertoma (imperfekti)

! Kieltomuodot (NEGATIVE-attribuutti)
[Et]  ! true
[Ef]  ! false
[Eb]  ! both

! Johdoksiin liittyvät liput
[Dg]  ! erisnimestä johdetaan yleisnimi
[De]  ! erisnimen etuliitteenä kirjoitettava isolla alkukirjaimella

! Lisätietoliput
[Ips] ! paikannimi taipuu sisäpaikallissijoissa
[Ipu] ! paikannimi taipuu ulkopaikallissijoissa
[Isf] ! sitaattilaina
[Icu] ! voidaan jatkaa yhdyssanana vain yhdysmerkin kanssa
[Ica] ! sallitaan yhdysmerkin puuttuminen [Icu]-lipusta huolimatta lippua edeltävällä rajalla (muttei myöhemmin)
[Ivj] ! vapaasti yhdistyvä yhdyssanan jälkiosa

! Rajamerkit
[Bc]  ! arvaamalla tuotetun yhdyssanan osien välinen raja
[Bh]  ! yhdysmerkki vokaaliehdolla
[Bm]  ! tunnettu morfeemiraja

! Korvattavat merkit
[[L]] ! edellisen merkin toisto
[[IE]] ! edeltävä i-kirjain muutetaan e-kirjaimeksi
[[H]] ! Lisätään h kahden saman ääntiön väliin vanhoissa taivutuksissa, esim. valo(h)on.
[[HV]] ! ö -> öhö jne.

! Diakriitit
@P.EI_YKS.ON@ @D.EI_YKS@ @C.EI_YKS@ ! Yksikkömuotoja ei sallita

@P.VAIN_YM3.ON@ @D.VAIN_YM3@ ! Persoona: vain yksikön tai monikon 3. sallittu

@D.YS_ALKANUT@ @R.YS_ALKANUT@ @P.YS_ALKANUT.ON@ ! Yhdyssana on alkanut
@P.YS_EI_JATKOA.ON@ @D.YS_EI_JATKOA@ @C.YS_EI_JATKOA@ ! Yhdyssanaa ei saa jatkaa
@P.YS_JATKETTAVA.ON@ @D.YS_JATKETTAVA@ @C.YS_JATKETTAVA@ ! Yhdyssanaa on jatkettava

! Jatko (esim. teonsana) on sallittu vain nimisanaksi johdettuna
@D.JOHDETTAVA_NIMISANAKSI@ @P.JOHDETTAVA_NIMISANAKSI.ON@ @C.JOHDETTAVA_NIMISANAKSI@

! Jatko (esim. teonsana) on sallittu vain laatusanaksi johdettuna
@D.JOHDETTAVA_LAATUSANAKSI@ @P.JOHDETTAVA_LAATUSANAKSI.ON@ @C.JOHDETTAVA_LAATUSANAKSI@

! Sana (nimi_laatusana) on tulkittava vain nimisanamerkityksessään
@P.VAIN_NIMISANA.ON@ @D.VAIN_NIMISANA@ @C.VAIN_NIMISANA@

! Sana on yksitekijäinen teonsana
@P.YKSITEKIJÄINEN.ON@ @D.YKSITEKIJÄINEN@

! inen-johdin aina sallittu
@P.INEN_SALLITTU.ON@ @R.INEN_SALLITTU@ @C.INEN_SALLITTU@

! inen-johdin aina kielletty
@P.INEN_KIELLETTY.ON@ @D.INEN_KIELLETTY@ @C.INEN_KIELLETTY@

! ei mAinen-johdosta
@P.EI_MAINEN.ON@ @D.EI_MAINEN@

! inen-johdin vaaditaan
@P.INEN_VAADITTU.ON@ @D.INEN_VAADITTU@ @C.INEN_VAADITTU@ @R.INEN_VAADITTU@

! lAinen-loppuinen sana (toistaiseksi vain nimisana)
@P.LAINEN.ON@ @R.LAINEN@

! lAinen-johdin vaaditaan
@P.LAINEN_VAADITTU.ON@ @D.LAINEN_VAADITTU@ @C.LAINEN_VAADITTU@

! ei lAinen-johdosta
@P.EI_LAINEN.ON@ @D.EI_LAINEN@

! etu/takavokaalimuodot sallittu
@P.V_SALLITTU.E@ @P.V_SALLITTU.T@ @R.V_SALLITTU.E@ @R.V_SALLITTU.T@ @C.V_SALLITTU@

! vertailumuodot eivät ole sallittuja
@P.EI_VERTM.ON@ @D.EI_VERTM@

! paikannimen jälkiliite vaaditaan
@P.PAIKANNIMEN_JL.ON@ @C.PAIKANNIMEN_JL@ @D.PAIKANNIMEN_JL@

! Lukusanojen taivutuksen rajoitus
@U.LS.SNNY@ @U.LS.SGNY@ @U.LS.SPNY@ @U.LS.STRNY@ @U.LS.SESNY@ @U.LS.SINENY@ @U.LS.SELANY@ @U.LS.SILLNY@
@U.LS.SADENY@ @U.LS.SABLNY@ @U.LS.SALLNY@ @U.LS.SABNY@
@U.LS.SNNM@ @U.LS.SGNM@ @U.LS.SPNM@ @U.LS.STRNM@ @U.LS.SESNM@ @U.LS.SINENM@ @U.LS.SELANM@ @U.LS.SILLNM@
@U.LS.SADENM@ @U.LS.SABLNM@ @U.LS.SALLNM@ @U.LS.SABNM@ @U.LS.SINNM@ @U.LS.SKONM@ @U.LS.SSTINY@

LEXICON Root
Sanasto	;
-[Bc]@P.YS_ALKANUT.ON@:-@P.YS_ALKANUT.ON@	Sanasto_ee	;
-[Bc]@P.YS_ALKANUT.ON@:-@P.YS_ALKANUT.ON@	Sanasto_em	;
-[Bc]@P.YS_ALKANUT.ON@:-@P.YS_ALKANUT.ON@	Sanasto_ep	;
-[Bc]@P.YS_ALKANUT.ON@:-@P.YS_ALKANUT.ON@	Sanasto_es	;
-[Bc]@P.YS_ALKANUT.ON@:-@P.YS_ALKANUT.ON@	Sanasto_a	;
-[Bc]@P.YS_ALKANUT.ON@:-@P.YS_ALKANUT.ON@	Sanasto_nimisanat	;
-[Bc]@P.YS_ALKANUT.ON@@P.JOHDETTAVA_NIMISANAKSI.ON@:-@P.YS_ALKANUT.ON@@P.JOHDETTAVA_NIMISANAKSI.ON@	Sanasto_t	;
-[Bc]@P.YS_ALKANUT.ON@@P.INEN_SALLITTU.ON@:-@P.YS_ALKANUT.ON@@P.INEN_SALLITTU.ON@	Sanasto_l	;
-[Bc]@P.YS_ALKANUT.ON@@P.INEN_VAADITTU.ON@:-@P.YS_ALKANUT.ON@@P.INEN_VAADITTU.ON@	Sanasto_nimisanat	;
LukusananAlku	;
-[Bc]@P.YS_ALKANUT.ON@:-@P.YS_ALKANUT.ON@	LukusananAlku	;
-:-	LukusananJälkiliiteEiYhdysmerkkiä	;
Asemosana	;
Suhdesana	;
Kieltosana	;

LEXICON Sanasto
Sanasto_ee	;
Sanasto_em	;
Sanasto_ep	;
Sanasto_es	;
Sanasto_h	;
Sanasto_l	;
Sanasto_n	;
Sanasto_nl	;
Sanasto_t	;
Sanasto_p	;
Sanasto_a	;
Sanasto_s	;
Sanasto_c	;

LEXICON Sanasto_ee
Joukahainen_ee	;

LEXICON Sanasto_em
Joukahainen_em	;
Poikkeavat_em	;

LEXICON Sanasto_ep
Joukahainen_ep	;
Poikkeavat_ep	;

LEXICON Sanasto_es
Joukahainen_es	;
Poikkeavat_es	;

LEXICON Sanasto_h
Joukahainen_h	;

LEXICON Sanasto_l
Joukahainen_l	;
Poikkeavat_l	;

LEXICON Sanasto_n
Joukahainen_n	;
Poikkeavat_n	;

LEXICON Sanasto_nl
Joukahainen_nl	;
Poikkeavat_nl	;

LEXICON Sanasto_t
Joukahainen_t	;
Poikkeavat_t	;

LEXICON Sanasto_p
Joukahainen_p	;

LEXICON Sanasto_a
Joukahainen_a	;
Poikkeavat_a	;

LEXICON Sanasto_s
Joukahainen_s	;
Poikkeavat_s	;

LEXICON Sanasto_c
Joukahainen_c	;

LEXICON Sanasto_laatusanat
Sanasto_l	;
Sanasto_nl	;

LEXICON Sanasto_nimisanat
Sanasto_n	;
@P.EI_VERTM.ON@@P.VAIN_NIMISANA.ON@:@P.EI_VERTM.ON@@P.VAIN_NIMISANA.ON@	Sanasto_nl	;
@P.JOHDETTAVA_NIMISANAKSI.ON@:@P.JOHDETTAVA_NIMISANAKSI.ON@	Sanasto_p	;

LEXICON Sanasto_nimisanat_ja_nl_vertailumuodot
Sanasto_n	;
@P.VAIN_NIMISANA.ON@:@P.VAIN_NIMISANA.ON@	Sanasto_nl	;
@P.JOHDETTAVA_NIMISANAKSI.ON@:@P.JOHDETTAVA_NIMISANAKSI.ON@	Sanasto_p	;

LEXICON Sanasto_nimisana_yhdyssanana_perusosana
Sanasto_nimisanat	;
Ys_perusosa_seikkasanasta	;

LEXICON Sanasto_nimisana_yhdyssanana_perusosana_ja_nl_vertailumuodot
Sanasto_nimisanat_ja_nl_vertailumuodot	;
Ys_perusosa_seikkasanasta	;

LEXICON Yleisnimet
Sanasto_l	;
Sanasto_nl	;
Sanasto_n	;

LEXICON Erisnimet
Sanasto_ee	;
Sanasto_em	;
Sanasto_ep	;
Sanasto_es	;
